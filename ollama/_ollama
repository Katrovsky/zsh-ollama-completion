#compdef ollama

_ollama() {
  local curcontext="$curcontext" state line
  typeset -A opt_args

  local -a commands
  commands=(
    'serve:Start ollama'
    'create:Create a model from a Modelfile'
    'show:Show information for a model'
    'run:Run a model'
    'pull:Pull a model from a registry'
    'push:Push a model to a registry'
    'list:List models'
    'ps:List running models'
    'cp:Copy a model'
    'rm:Remove a model'
    'help:Help about any command'
  )

  _arguments -C \
    '1: :->command' \
    '*:: :->args'

  case $state in
    (command)
      _describe -t commands 'ollama commands' commands
      ;;
    (args)
      case $line[1] in
        (run|pull|show|rm|cp)
          local -a models
          models=(${(f)"$(ollama list 2>/dev/null | tail -n +2 | awk -F'\t' '{gsub(/ +$/, "", $1); print $1}')"})
          compadd -a models
          ;;
        (create)
          _arguments '-f[Specify Modelfile]:filename:_files'
          ;;
        (push)
          _arguments '1:model name' '2:destination'
          ;;
        (list|ps|serve)
          ;;
        (help)
          _describe -t commands 'ollama commands' commands
          ;;
      esac
      ;;
  esac
}

_ollama "$@"